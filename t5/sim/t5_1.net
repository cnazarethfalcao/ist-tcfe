*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends



.options savecurrents

.options numdgt = 12


Vcc vcc 0 10.0
Vee vee 0 -10.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)

X1 3 2 vcc vee 4 uA741

C1 in 3 220n

R1 3 0 1k

****
R3 5 4 100k

R5 2 5 100k

*R6 2 5 10k
****

R4 2 0 1k

R2 4 out 1k

C2 out 0 110n

.op
.end

.control



.op


print all


ac dec 10 10 100MEG
hardcopy vo1f.ps vdb(out) 
hardcopy vo2f.ps 180/PI*phase(v(out))




let threshold = vecmax(vdb(out))-3
meas ac lcf WHEN vdb(out) = threshold RISE = 1
meas ac ucf WHEN vdb(out) = threshold CROSS = LAST

let Cfreq = sqrt(lcf*ucf)

meas AC maximo MAX vdb(out) from=10 to=100MEG
*meas AC Cfreq MAX_AT vdb(out) from=10 to=100MEG

let gp=abs(40-maximo)
let cost = 13626.44-100-0.11
let cdevi=abs(1000-Cfreq)
let gaindevi=10^(gp/20)
let merit = 1/(cost*(gaindevi+cdevi+10^-6))




*input impedance in ohm
echo "Zin_TAB"
let Zr_in= Re(-v(in)[40]/i(Vin)[40])
let Zim_in= Im(-v(in)[40]/i(Vin)[40])
let Zin=abs(-v(in)[40]/i(Vin)[40])
echo "Zin = $&Zin"
echo "Zin_END"


set numdgt = 16

echo "frs_TAB"
echo "Lower Cut-off frequency [Hz] = $&lcf"
echo "Upper Cut-off frequency [Hz] = $&ucf"
echo "Central Frequency [Hz] = $&Cfreq"
echo "Gain [dB] = $&maximo"
echo "frs_END"


echo "**************"
echo "results_TAB"
echo "Gain deviation [V] = $&gaindevi"
echo "Central frequency deviation [Hz] = $&cdevi"
echo "Cost = $&cost"
echo "Merit = $&merit"
echo "results_END"
echo "**************"



quit
.endc 
.end
